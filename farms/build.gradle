/*
 * -------------------------------------
 * Copyright Lorenzo0111 2024
 * https://github.com/Lorenzo0111
 * -------------------------------------
 */
apply plugin: 'com.github.johnrengelman.shadow'

import org.apache.tools.ant.filters.ReplaceTokens

repositories {
    mavenCentral()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
    maven { url = 'https://jitpack.io' }
    maven { url = "https://repo.codemc.io/repository/maven-public/" }
    maven { url = "https://maven.enginehub.org/repo/" }
}

dependencies {
    compileOnly "org.jetbrains:annotations:24.1.0"
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor "org.projectlombok:lombok:1.18.32"

    compileOnly "com.sk89q.worldguard:worldguard-bukkit:7.0.9"
    compileOnly "com.github.MilkBowl:VaultAPI:1.7"

    compileOnly "org.spigotmc:spigot-api:1.20.6-R0.1-SNAPSHOT"

    implementation project(":farms-api")

    implementation "org.bstats:bstats-bukkit:3.0.2"
    implementation "de.tr7zw:item-nbt-api:2.12.4"
    implementation 'net.kyori:adventure-api:4.17.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.3.2'
    implementation 'dev.triumphteam:triumph-gui:3.1.7'
    implementation 'com.github.cryptomorin:XSeries:10.0.0'
    implementation("com.github.l1m0n3:HexiTextLib:b9e30055b7") {
        exclude module: "annotations"
    }
}

jar {
    archiveBaseName.set("Farms")
}

shadowJar {
    archiveBaseName.set("Farms")

    relocate("org.bstats", "me.lorenzo0111.farms.bstats")
    relocate("com.haroldstudios.hexitextlib", "me.lorenzo0111.farms.libs.hex")
    relocate("de.tr7zw.annotations", "me.lorenzo0111.farms.libs.annotations")
    relocate("de.tr7zw.changeme.nbtapi","me.lorenzo0111.farms.libs.nbt")
    relocate("dev.triumphteam.gui", "me.lorenzo0111.farms.gui")
    relocate("net.kyori.examination","me.lorenzo0111.farms.libs.examination")
    relocate("net.kyori.adventure","me.lorenzo0111.farms.libs.adventure")
    relocate("com.cryptomorin.xseries","me.lorenzo0111.farms.libs.xseries")
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        duplicatesStrategy DuplicatesStrategy.INCLUDE
        filter ReplaceTokens, tokens: [version: version]
    }
}